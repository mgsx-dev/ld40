
buildscript {
	repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
    	classpath "com.badlogicgames.gdx:gdx-tools:1.9.6"
    }
	apply from: 'gradle/tiled.gradle'
	apply from: 'gradle/hiero.gradle'
	apply from: 'gradle/atlas.gradle'
	apply from: 'gradle/raw.gradle'
	apply from: 'gradle/gimp.gradle'
	apply from: 'gradle/inkscape.gradle'
	apply from: 'gradle/blender.gradle'
}


apply plugin: 'base' 

eclipse.project {
    name = appName + "-assets"
}

def assetsDir = "$projectDir/../android/assets"

def assetsTasks = []

assetsTasks << blenderRenderFrame("src/tileset.blend", 1, "$assetsDir/tileset.png")

def mushroomFramesTask = blenderRenderFrame("src/mushroom.blend", 1, "$buildDir/enemies/mushroom.png")
def lifeFramesTask = blenderRenderFrame("src/life.blend", 1, "$buildDir/enemies/life.png")
def ballsFramesTask = blenderRenderAnimation("src/balls.blend", "$buildDir/enemies/balls")

def enemiesPackTask = gdxAtlasPack("$buildDir/enemies", "../android/assets/enemies")
enemiesPackTask.dependsOn += [mushroomFramesTask, ballsFramesTask, lifeFramesTask]


def heroFramesTask = blenderRenderAnimation("src/hero.blend", "$buildDir/hero/hero")
def tailFramesTask = blenderRenderAnimation("src/tail.blend", "$buildDir/hero/tail")

def heroAnimTask = gdxAtlasPack("$buildDir/hero", "../android/assets/hero")
heroAnimTask.dependsOn += [heroFramesTask, tailFramesTask]

assetsTasks << heroAnimTask
assetsTasks << enemiesPackTask





/////////////////////// SKIN

def assetsPrepareTasks = []

assetsPrepareTasks << gdxHieroExport("src/skin/default.hiero", "$buildDir/skin")

assetsPrepareTasks << gdxRawCopy("src/skin", "$buildDir/skin")

// assetsPrepareTasks << gimpExportLayers("assets-src/skins/hud-skin/bt.xcf", "$buildDir/skins/hud-skin/unpacked")

assetsPrepareTasks << inkscapeExport("src/skin/panel.9.svg", "$buildDir/skin")
assetsPrepareTasks << inkscapeExport("src/skin/bt-up.9.svg", "$buildDir/skin")
assetsPrepareTasks << inkscapeExport("src/skin/heart.svg", "$buildDir/skin")

def packTask = gdxAtlasPack("$buildDir/skin", "../android/assets/skin/skin")

packTask.dependsOn += assetsPrepareTasks

assetsTasks << packTask



///////////////////////////


task generateAssets(dependsOn: assetsTasks, group: "gdx"){}
